apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'

// Some of our secrets live in AdConstants.kt. Some of them need to live in the app manifest instead of code.
// This lets us access secret keys stored in the private assets repo from within this build config,
// which in turn can be used to set manifestPlaceholders below so we can read them in AndroidManifest.xml
def constantsFile = rootProject.file("../assets/config/androidKeys.properties");
def constants = new Properties()
constants.load(new FileInputStream(constantsFile))

repositories {
    jcenter() // includes the MoPub SDK and AVID library
    maven { url "https://s3.amazonaws.com/moat-sdk-builds" }
    maven { url 'https://jitpack.io' }
    maven { url "https://dl.bintray.com/ironsource-mobile/android-sdk" }
    maven { url 'https://dl.bintray.com/ironsource-mobile/android-adapters' }

    google()

}

android {
    signingConfigs {
        playstore {
            storeFile file(constants["storeFile"])
            storePassword constants["storePassword"]
            keyAlias constants["keyAlias"]
            keyPassword constants["keyPassword"]
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lazerwalker.flappyroyale"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 13
        versionName "1.2"
        signingConfig signingConfigs.playstore
        manifestPlaceholders = [
                AdmobAppId: constants["AdmobAppId"]
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Without this, we get odd Dex compilation errors.
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation project(path: ':flappyAdConstants')

    implementation('com.mopub:mopub-sdk:+@aar') {
        transitive = true
    }

    implementation 'com.google.firebase:firebase-core:16.0.9'

//    implementation 'com.google.android.gms:play-services-games:18.0.0'

    implementation 'com.jaredrummler:android-device-names:1.1.9'


    /** IronSource ads and mediations*/
    implementation 'com.ironsource.sdk:mediationsdk:6.8.5@jar'

    // Add AdMob Network
    implementation 'com.google.android.gms:play-services-ads:17.2.0'
    implementation 'com.ironsource.adapters:admobadapter:4.3.3@jar'

    // Add Applovin Network
    implementation 'com.ironsource.adapters:applovinadapter:4.3.4@jar'

    // Add Chartboost Network
    implementation 'com.ironsource.adapters:chartboostadapter:4.1.9@jar'

    // Add Facebook Network
    implementation 'com.ironsource.adapters:facebookadapter:4.3.3@jar'
    implementation 'com.facebook.android:audience-network-sdk:5.3.1'
    implementation 'com.android.support:recyclerview-v7:25.3.1'

    // Add Tapjoy Network
    implementation 'com.ironsource.adapters:tapjoyadapter:4.1.5@jar'

    // Add UnityAds Network
    implementation 'com.ironsource.adapters:unityadsadapter:4.1.4@jar'

    // Add Vungle Network
    implementation 'com.ironsource.adapters:vungleadapter:4.1.5@jar'
    implementation 'com.github.vungle:vungle-android-sdk:6.3.24'

}
