apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'

// Some of our secrets live in AdConstants.kt. Some of them need to live in the app manifest instead of code.
// This lets us access secret keys stored in the private assets repo from within this build config,
// which in turn can be used to set manifestPlaceholders below so we can read them in AndroidManifest.xml
def constantsFile = rootProject.file("../assets/config/androidKeys.properties");
def constants = new Properties()
constants.load(new FileInputStream(constantsFile))

repositories {
    jcenter() // includes the MoPub SDK and AVID library
    maven { url "https://s3.amazonaws.com/moat-sdk-builds" }
    maven { url 'https://adcolony.bintray.com/AdColony' }
    maven { url 'https://jitpack.io' }
}

android {
    signingConfigs {
        playstore {
            storeFile file(constants["storeFile"])
            storePassword constants["storePassword"]
            keyAlias constants["keyAlias"]
            keyPassword constants["keyPassword"]
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lazerwalker.flappyroyale"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 8
        versionName "1.0"
        manifestPlaceholders = [
                ApplovinSdkKey: constants["ApplovinSdkKey"],
                AdmobAppId: constants["AdmobAppId"]
        ]
        signingConfig signingConfigs.playstore
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Without this, we get odd Dex compilation errors.
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation project(path: ':flappyAdConstants')

    implementation('com.mopub:mopub-sdk:+@aar') {
        transitive = true
    }

    implementation 'com.google.firebase:firebase-core:16.0.9'

    // Chartboost
    // This mediated network SDK is not available on JCenter. Ensure you have downloaded and imported it manually into your app project.
    implementation 'com.mopub.mediation:chartboost:7.3.1.4'

    // AppLovin
    implementation 'com.applovin:applovin-sdk:9.7.1'
    implementation 'com.mopub.mediation:applovin:9.7.1.0'

    // AdColony
    implementation 'com.adcolony:sdk:3.3.10'
    implementation 'com.mopub.mediation:adcolony:3.3.10.0'

    // Google (AdMob)
    implementation 'com.google.android.gms:play-services-ads:17.2.1'
    implementation 'com.mopub.mediation:admob:17.2.1.0'

    // Vungle
    implementation 'com.github.vungle:vungle-android-sdk:6.3.24'
    implementation 'com.mopub.mediation:vungle:6.3.24.3'

    implementation 'com.jaredrummler:android-device-names:1.1.9'


    // Google Play Services needed by Chartboost
//    implementation "com.google.android.gms:play-services-base:$project.ext.googlePlayServicesVersion"
//    implementation "com.google.android.gms:play-services-ads-identifier:$project.ext.googlePlayServicesVersion"
}
